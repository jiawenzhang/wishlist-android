apply plugin: 'com.android.application'
import javax.crypto.spec.IvParameterSpec
import javax.crypto.spec.SecretKeySpec
import javax.crypto.Cipher

android {
    compileSdkVersion 24
    buildToolsVersion "23.0.2"
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "com.wish.wishlist"
        // Parse Android SDK requires GET_ACCOUNTS permission for 4.0.3 (API 15) and lower
        // set minSdkVersion to 16 so we do not need to request GET_ACCOUNTS permission
        minSdkVersion 16
        targetSdkVersion 23

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    sourceSets {
        main {
            java {
                exclude '**/FacebookPostActivity.java'
                exclude '**/PostToSNSActivity.java'
            }
        }
    }
    
    signingConfigs {
        release {
            //The following parameters should be defined in ~/.gradle/gradle.properties
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
        debug {
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
            //applicationIdSuffix '.release'
            //versionNameSuffix '-release'
        }
        debug {
            debuggable true
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
    aaptOptions {
        noCompress "js"
    }

    ext {
        KEY = "d214e8a3af797fed".padRight(16).getBytes("UTF-8")
        IV = "56e27baf5903c24d".padRight(16).getBytes("UTF-8")
    }

    task encryptJS << {
        SecretKeySpec key = new SecretKeySpec(KEY, "AES")
        Cipher cipher = Cipher.getInstance("AES/CBC/PKCS5Padding")
        cipher.init(Cipher.ENCRYPT_MODE, key, new IvParameterSpec(IV))
        String fileContents = new File('third-party/wish-scraper/mobile_test.js').getText('UTF-8')
        def encBytes = cipher.doFinal(fileContents.getBytes("UTF-8"))
        def out_t = file('src/main/assets/t')
        out_t.delete()
        out_t << encBytes

        String f1 = new File('third-party/wish-scraper/currency_symbol_map.js').getText('UTF-8')
        String f2 = new File('third-party/wish-scraper/accounting.js').getText('UTF-8')
        String f3 = new File('third-party/wish-scraper/util.js').getText('UTF-8')
        String f4 = new File('third-party/wish-scraper/scrape.js').getText('UTF-8')
        def encBytesJ = cipher.doFinal((f1+f2+f3+f4).getBytes("UTF-8"))
        def out_j = file('src/main/assets/j')
        out_j.delete()
        out_j << encBytesJ
    }

    preBuild.dependsOn(encryptJS)
}

dependencies {
    compile 'org.apache.commons:commons-lang3:3.4'
    compile project(':third-party:TokenAutoComplete:library')
    compile project(':third-party:Android-Cloud-TagView-Plus:library')
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.github.bumptech.glide:glide:3.5.2'
    compile files('libs/jsoup-1.8.1.jar')
    compile 'com.viewpagerindicator:library:2.4.1@aar'
    compile 'com.isseiaoki:simplecropview:1.0.8'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.birbit:android-priority-jobqueue:1.3.5'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.bignerdranch.android:recyclerview-multiselect:0.2'
    compile 'com.github.ksoichiro:android-observablescrollview:1.6.0'
    compile 'com.github.stkent:amplify:1.0.0'
    compile 'com.github.chrisbanes:PhotoView:1.2.6'
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'

    compile 'com.parse.bolts:bolts-android:1.3.0'
    compile 'com.facebook.android:facebook-android-sdk:4.13.1'
    compile 'com.parse:parsefacebookutils-v4-android:1.10.4@aar'
    compile 'com.parse:parsetwitterutils-android:1.10.5'
    compile 'com.parse:parse-android:1.13.0'
    debugCompile 'com.parse:parseinterceptors:0.0.2'

    compile 'com.android.support:recyclerview-v7:24.2.1'
    compile 'com.android.support:cardview-v7:24.2.1'
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:support-v4:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.google.android.gms:play-services-maps:8.3.0'
    compile 'com.google.android.gms:play-services-analytics:8.3.0'
    compile 'com.takisoft.fix:preference-v7:23.4.0.4'
    compile 'de.sven-jacobs:loremipsum:1.0'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.github.evgenyneu:js-evaluator-for-android:v1.0.7'

    // Local unit test
    // Required -- JUnit 4 framework
    testCompile 'junit:junit:4.12'
    // Optional -- Mockito framework
    testCompile 'org.mockito:mockito-core:1.10.19'

    // Instrumented test
    androidTestCompile 'com.android.support:support-annotations:24.2.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    // Optional -- Hamcrest library
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    // Optional -- UI testing with Espresso
    //androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    // Optional -- UI testing with UI Automator
    //androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.1'
}
